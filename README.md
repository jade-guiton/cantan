# cantan

A toy language VM

This language is dynamically typed, and uses a hybrid stack/register VM similar to Lua.

## CLI usage

```
Usage:
  cantan parse <src>
  cantan compile <src>
  cantan run <src>
  cantan repl [-d]
  cantan --help | -h

Arguments:
  <src>        Path to a Cantan source file (usually .cn)

Options:
  --help, -h   Print this help message
  -d           Show run bytecode
```

Note that "compile" displays the generated bytecode, but there is no way to store/load bytecode from a file.

## Examples

**Fizzbuzz:**
```
for(n in range(100))
	let res = ""
	if(n % 3 == 0)
		res += "Fizz"
	end
	if(n % 5 == 0)
		res += "Buzz"
	end
	if(res == "")
		res += repr(n)
	end
	term.writeln(res)
end
```

**Quicksort:**
```
let quicksort(l)
	if(l.size() <= 1) ret l end
	
	let pivot = l[l.size() // 2]
	
	let smaller = []
	let equal = []
	let greater = []
	for(x in l)
		if(x < pivot)
			smaller.push(x)
		elseif(x > pivot)
			greater.push(x)
		else
			equal.push(x)
		end
	end
	
	ret quicksort(smaller) + equal + quicksort(greater)
end

let l = [5, 7, 8, 10, 1, 2]
term.log(quicksort(l))
```

**Struct demo:**
```
let Person(name, age)
	let self = {
		name = name,
		age = age,
		talk()
			term.writeln("What up, I'm " + self.name + ", I'm " + repr(self.age))
		end,
		grow_up()
			self.age = self.age + 1
		end,
	}
	ret self
end

let me = Person("Jared", 19)
me.talk()
me.grow_up()
me.talk()
```
